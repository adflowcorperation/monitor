###########################################service############################################################
RULE start
CLASS javax.servlet.http.HttpServlet
METHOD service(javax.servlet.http.HttpServletRequest, javax.servlet.http.HttpServletResponse)
HELPER  kr.co.adflow.monitor.helper.WebHelper
AT ENTRY
BIND threadId:String = Long.toString(Thread.currentThread().getId()),
     threadName:String = Thread.currentThread().getName(),
     requsetURL:StringBuffer = $1.getRequestURL()
IF true
DO startTime(threadId)
ENDRULE
#######################################################################################################
RULE stop
CLASS javax.servlet.http.HttpServlet
METHOD service(javax.servlet.http.HttpServletRequest, javax.servlet.http.HttpServletResponse)
HELPER  kr.co.adflow.monitor.helper.WebHelper
AT EXIT
BIND threadId:String = Long.toString(Thread.currentThread().getId()),
     threadName:String = Thread.currentThread().getName(),
     requsetURL:StringBuffer = $1.getRequestURL()

IF true
DO stopTime(threadId,requsetURL)
ENDRULE

#########################################################################
# monitor connection leak start point
RULE getConnection rule
INTERFACE ^javax.sql.DataSource
METHOD getConnection
HELPER kr.co.adflow.monitor.helper.JdbcTracerHelper
AT EXIT
IF  true
DO	registerOpenedConnection($!)
ENDRULE

#########################################################################
# monitor connection leak caught SQLException
RULE be caught exception when connectting
CLASS ^java.sql.SQLException
METHOD <init>
HELPER kr.co.adflow.monitor.helper.JdbcTracerHelper
AT ENTRY
IF TRUE
DO  getSQLException($0)
ENDRULE

#########################################################################
# monitor connection leak close point
RULE Connection  close Rule
INTERFACE ^java.sql.Connection
METHOD close
HELPER kr.co.adflow.monitor.helper.JdbcTracerHelper
AT ENTRY
IF TRUE
DO  releaseConnection()
ENDRULE

#########################################################################
# get query start point
RULE connection start time
INTERFACE ^javax.sql.DataSource
METHOD getConnection
HELPER  kr.co.adflow.monitor.helper.JdbcTracerHelper
AT ENTRY
IF TRUE
DO getConnectionTime()
ENDRULE

#########################################################################
# get query stop point
RULE connection stop time
INTERFACE ^java.sql.Connection
METHOD close
HELPER kr.co.adflow.monitor.helper.JdbcTracerHelper
AT EXIT
IF TRUE
DO releaseConnectionTime()
ENDRULE

#########################################################################
# get query rule 1 (for ibatis, oracle)
RULE  PreparedStatement getQuery Rule
INTERFACE ^java.sql.Connection
METHOD prepareStatement(java.lang.String)
HELPER  kr.co.adflow.monitor.helper.JdbcTracerHelper
AT ENTRY
BIND queryString : String = $1
IF queryString != null
DO  executeQueryByteman($1)
ENDRULE

#########################################################################
# get query rule 2 (for oracle)
RULE Statement getQuery Rule
INTERFACE ^java.sql.Statement
METHOD executeQuery(java.lang.String)
HELPER kr.co.adflow.monitor.helper.JdbcTracerHelper
AT ENTRY
IF true
	DO  executeQueryByteman($1)
ENDRULE



#########################################################################
# Exception track Rule
RULE catch exception Rule
CLASS ^java.lang.Exception
METHOD <init>
HELPER kr.co.adflow.monitor.helper.ExceptionTraceHelper
AT ENTRY
BIND exception = $this
IF TRUE
DO needIntercepting(exception)
ENDRULE